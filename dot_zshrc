function try_source { if [[ -e "$1" ]]; then source "$1"; fi }
function exists { command -v "$1" >/dev/null 2>&1; return $? }

# TODO: Support multiple additional imports
try_source "$ZSH_ADDITIONAL_IMPORTS"

if [[ -z "$DEFAULT_USER" ]]; then
  DEFAULT_USER=wesr
fi

# Set SSH information in a friendlier format
export SSH_PKEY="$HOME/.ssh/rsa_id"
export SESSION_TYPE="$(who -m | awk '{ print $2 }' | sed 's/[0-9]*$//')"
if [[ -n "$SSH_CLIENT" || -n "$SSH_TTY" ]]; then
  export SESSION_TYPE=remote/ssh
else
  case $(ps -o comm= -p $PPID) in
    sshd|*/sshd) export SESSION_TYPE=remote/ssh ;;
  esac
fi

# ------------------------------------------------------------------------------
# ----------- Run this check before setting up the rest of the shell -----------
# ------------ so that we don't do extra work if we're relaunching. ------------
# ------------------------------------------------------------------------------

if exists zellij && [[ ("$AUTO_LAUNCH_WORKSPACE" == 'zellij' || -z "$AUTO_LAUNCH_WORKSPACE") ]]; then
  # TODO: Only auto-attach if the session has no attached clients
  # export ZELLIJ_AUTO_ATTACH=true
  export ZELLIJ_AUTO_EXIT=true
  eval "$(zellij setup --generate-auto-start zsh)"
elif exists tmux; then
  if [[ -z "$TMUX" && ("$AUTO_LAUNCH_WORKSPACE" == 'tmux' || "$SESSION_TYPE" == 'remote/ssh') ]]; then
    sid="$(tmux ls | sed '/(attached)$/d' | sed 's/:.*//' | tail -n 1)"
    tmux ${sid:+attach} ${sid:+-t} $sid
    exit
  fi
  alias detach='tmux detach'
  if exists tmuxinator; then
    alias tx=tmuxinator
    function txx {
      session_name="$(tmux display-message -p '#S')"
      if [[ -n "$session_name" ]]; then
        echo "Stopping '$session_name' session..."
        tmuxinator stop "$session_name"
      else
        echo "No current session found!"
      fi
    }
  fi
fi

# ------------------------------------------------------------------------------
# ---------------- Remaining shell setup goes after this line. -----------------
# ------------------------------------------------------------------------------

function get-theme {
  theme='dark'
  if [[ $(uname) == "Darwin" ]]; then # MacOS
    theme="$(defaults read -globalDomain AppleInterfaceStyle &> /dev/null && echo dark || echo light)"
  elif [[ "$(uname -r | sed 's/.*-//')" == "WSL2" ]]; then # Windows::WSL2
    theme="$(reg.exe Query 'HKCU\SOFTWARE\Microsoft\Windows\CurrentVersion\Themes\Personalize' /v AppsUseLightTheme | sed -e 's/0x0/dark/; s/0x1/light/' -e '/^[[:space:]]*$/d' | awk '{print $3}' | xargs)"
  fi
  echo "$theme"
}

function cd {
  if [[ "$#" == '0' ]]; then
    pushd "$HOME" > /dev/null
  else
    pushd "$@" > /dev/null
  fi
}

function od {
  popd "$@" > /dev/null
}

function .. {
  dir='..'
  times=${1:-1}
  while [[ $((times--)) > 1 ]]; do
    dir="$dir/.."
  done
  cd $dir
}

function kill-port {
    echo "Killing $(lsof -t -i tcp:$1 | wc -l) processes"
    lsof -t -i tcp:$1 | xargs kill -9
    echo "$(lsof -t -i tcp:$1 | wc -l) processes remaining"
}

if exists zellij; then
  if [ -d "$HOME/.config/zellij/completions" ]; then
    FPATH="$HOME/.config/zellij/completions:$FPATH"
  fi
  alias zx='zellij --layout'
  function zr () { zellij run --name "$*" -- zsh -ic "$*";}
  function zrf () { zellij run --name "$*" --floating -- zsh -ic "$*";}
  function zri () { zellij run --name "$*" --in-place -- zsh -ic "$*";}
  function ze () { zellij edit "$*";}
  function zef () { zellij edit --floating "$*";}
  function zei () { zellij edit --in-place "$*";}
  function zpipe () {
    if [ -z "$1" ]; then
      zellij pipe;
    else
      zellij pipe -p $1;
    fi
  }
fi

if exists fzf; then
  export FZF_COMPLETION_TRIGGER='qq'
  export FZF_DEFAULT_COMMAND='rg --files --hidden --follow 2>/dev/null'
  export FZF_DEFAULT_OPTS="--height 40% --reverse --border"
  if [[ -f "$HOME/.fzf.zsh" ]]; then
    source "$HOME/.fzf.zsh"
  fi

  function fzf() {
    if [[ $(tput cols) > 100 ]]; then
      command fzf --preview '[[ $(file --mime {}) =~ binary ]] &&
                 echo {} is a binary file ||
                 (highlight -O ansi -l {} ||
                  coderay {} ||
                  rougify {} ||
                  cat {}) 2> /dev/null | head -255' "$@"
    else
      command fzf "$@"
    fi
  }

  alias fzh="fzf --history '${FZF_HISTORY_DIR:-TMPDIR}'"

  [ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
fi

export ZSH="$HOME/.oh-my-zsh"
if [[ -f "$ZSH/oh-my-zsh.sh" ]]; then
  # Backup variables used in the OH MY ZSH that need to be restored
  ENV_HOME=$HOME
  ENV_ZSH_THEME=$ZSH_THEME
  ENV_ZSH=$ZSH
  ZSH="$HOME/.oh-my-zsh"

  export ZSH_DISABLE_COMPFIX=true
  export KEYTIMEOUT=1

  plugins=(
    1password        # "op" wrapper to copy passwords from the command line
    aliases          # Help for finding aliases
    copypath         # Copy pwd or dir to clipboard
    git-commit       # Aliases for conventional commit messages
    last-working-dir # Automatically reopen in last working dir
    safe-paste       # Paste doesn't automatically execute code
    ssh-agent        # Automate ssh-agent launch and credential load
    tmux             # Tmux helpers and aliases
    tmuxinator       # Tmuxinator aliases
    vi-mode          # Vi keybindings in the terminal
    wd               # "Warp directory": Mark and cd to frequent directories
  )

  zstyle :omz:plugins:ssh-agent quiet yes

  [[ "$OSTYPE" == "darwin"* ]] && plugins+=(macos) # term/finder/music/etc app integrations
  exists fzf                   && plugins+=(fzf)
  exists jj                    && plugins+=(jj)

  alias mark='wd add'
  alias marks='wd list'
  alias delmark='wd rm'

  try_source $ZSH/oh-my-zsh.sh

  # Restore environment variables
  ZSH=${ENV_ZSH:-$ZSH}
  HOME=${ENV_HOME:-$HOME}
  ZSH_THEME=${ENV_ZSH_THEME:-$ZSH_THEME}
fi

# 0 -- vanilla completion (abc => abc)
# 1 -- smart case completion (abc => Abc)
# 2 -- word flex completion (abc => A-big-Car)
# 3 -- full flex completion (abc => ABraCadabra)
zstyle ':completion:*' matcher-list '' \
  'm:{a-z\-}={A-Z\_}' \
  'r:[^[:alpha:]]||[[:alpha:]]=** r:|=* m:{a-z\-}={A-Z\_}' \
  'r:|?=** m:{a-z\-}={A-Z\_}'

CASE_SENSITIVE='false'
HYPHEN_INSENSITIVE='true'
ENABLE_CORRECTION='true'
COMPLETION_WAITING_DOTS='true'
setopt globdots

[ -f "$HOME/.config/op/plugins.sh" ] && source "$HOME/.config/op/plugins.sh"
[ -d "$HOME/.docker/completions" ] && FPATH="$HOME/.docker/completions:$FPATH"

[ -f "$HOME/.local/bin/mise" ] && MISE_INSTALL="$HOME/.local/bin/mise"
[ -f "/opt/homebrew/bin/mise" ] && MISE_INSTALL="/opt/homebrew/bin/mise"
if [ -f "$MISE_INSTALL_DIR" ]; then
  export MISE_CONFIG_DIR="$HOME/.config/mise"
  eval "$($MISE_INSTALL activate zsh)"
fi

if exists nvim; then
  export EDITOR=nvim
  alias vim="$EDITOR"
  alias nvimrc="cd $HOME/.config/nvim && nvims settings"
  alias eds=nvims
else
  export EDITOR=vim
fi

alias e="$EDITOR"
alias ed="$EDITOR"
alias lg='lazygit'
alias gitk='gitk &'
alias beep='tput bel'
alias settings='ed ~/.zshrc'

export LESS="-F -X -R $LESS"

export SSH_COMMAND="$(which ssh)"
function ssh {
  if [[ -n "$ITERM_SHELL_INTEGRATION_INSTALLED" ]]; then
    export iterm2_hostname="${@:-1}"
    iterm2_print_state_data
    "$SSH_COMMAND" "$@"
    export iterm2_hostname="$(hostname)"
    iterm2_print_state_data
  else
    "$SSH_COMMAND" "$@"
  fi
}

if exists lazygit; then
  function lazygit {
    LAZY_THEME="$(get-theme)" command lazygit "$@"
  }
fi

if [ -d /mnt/c/QMK_MSYS ]; then
  # USB integrations on WSL are inadequate for QMK, use the Windows utils directly.
  export DFU_PROGRAMMER=/mnt/c/QMK_MSYS/mingw64/bin/dfu-programmer.exe
  export DFU_UTIL=/mnt/c/QMK_MSYS/mingw64/bin/dfu-util.exe
  export TEENSY_LOADER_CLI=/mnt/c/QMK_MSYS/mingw64/bin/teensy_loader_cli.exe
  export BATCHISP=/mnt/c/QMK_MSYS/mingw64/bin/batchisp.exe
fi

eval "$(starship init zsh)"

try_source "$HOME/.zshrc.after"